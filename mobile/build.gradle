apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'org.greenrobot.greendao'

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    buildTypes {

        debug {
            testCoverageEnabled true
            minifyEnabled false
            useProguard false
            buildConfigField("String", "BASE_URL", "\"http://www.mocky.io/v2\"")
            buildConfigField("String", "API_KEY", "\"ABCXYZ123TEST\"")
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BASE_URL", "\"http://www.mocky.io/v2\"")
            buildConfigField("String", "API_KEY", "\"ABCXYZ123TEST\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.uele.reidx.android"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        javaMaxHeapSize "4g" //specify the heap size for the dex process
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        pickFirst 'lib/armeabi-v7a/libmp3lame.so'
    }

    productFlavors {

        leads {
            applicationId 'com.uele.reidx.android.leads'
            versionCode 1
            versionName '1.0.0'
        }

        investors {
            applicationId 'com.uele.reidx.android.investors'
            versionCode 1
            versionName '1.0.0'
        }
    }

    signingConfigs {
        //Uncomment and set values and then this will work
        //release {
              //storeFile file(System.getenv('ANDROID_KEYSTORE_PATH'))
              //storePassword System.getenv('ANDROID_STORE_PASS')
              //keyAlias System.getenv('ANDROID_KEY_ALIAS')
              //keyPassword System.getenv('ANDROID_KEY_PASS')
            // }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // android support libraries
    compile "com.android.support:animated-vector-drawable:$rootProject.supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:preference-v14:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-core-utils:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-vector-drawable:$rootProject.supportLibraryVersion"

    // code generator for view
    compile "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // crash reporting
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }

    // database
    compile "org.greenrobot:greendao:$rootProject.greenDaoVersion"

    // dependency injection
    compile "com.google.dagger:dagger:$rootProject.dagger2Version"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'javax.inject:javax.inject:1'

    // font
    compile "uk.co.chrisjenx:calligraphy:$rootProject.calligraphyVersion"

    // parser
    compile "com.google.code.gson:gson:$rootProject.gsonVersion"

    // Events
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.squareup:otto:1.3.8'

    // debug database
    debugCompile "com.amitshekhar.android:debug-db:$rootProject.debugDBVersion"

    // image
    compile "com.github.bumptech.glide:glide:$rootProject.glideVersion"

    // network
    compile "com.amitshekhar.android:rx2-android-networking:$rootProject.rx2AndroidNetworking"

    // logger
    compile "com.jakewharton.timber:timber:$rootProject.timberVersion"

    // reactive
    compile "io.reactivex.rxjava2:rxjava:$rootProject.rxjava2Version"
    compile "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"

    compile 'com.jakewharton.byteunits:byteunits:0.9.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'net.danlew:android.joda:2.9.9'

    // dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junitVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"

    // UI Testing
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"
    androidTestCompile "com.google.dexmaker:dexmaker:$dexMakerVersion"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:$dexMakerVersion"
    androidTestCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"

    wearApp project(':wear')
    compile 'com.google.android.gms:play-services-wearable:11.0.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
}

greendao {
    schemaVersion 1
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply from: 'qa-check.gradle'
